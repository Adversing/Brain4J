plugins {
    id "java"
    alias libs.plugins.shadowJar
}

dependencies {
    implementation project(":brain4j-math")

    implementation libs.slf4jApi
    implementation libs.logbackClassic

    implementation libs.httpclient5
    implementation libs.jacksonDatabind
    implementation libs.typesafeConfig
    implementation libs.commonsCompress
    implementation libs.pngj

    implementation libs.parquetAvro

    implementation libs.hadoopCommon
    implementation libs.hadoopMapreduce
}

shadowJar {
    minimize()
    archiveFileName = "brain4j-datasets-${parent.version}.jar"
}

tasks.register("cleanDatasetsCache") {
    description = "Cleans the local Brain4J datasets cache"
    group = "development"
    doLast {
        def cacheDir = new File(System.getProperty("user.home"), ".cache/brain4j/datasets")
        if (cacheDir.exists()) {
            delete cacheDir
            println "Cleaned cache directory: ${cacheDir.absolutePath}"
        } else {
            println "Cache directory does not exist: ${cacheDir.absolutePath}"
        }
    }
}
